// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

buildscript {
    ext {
        target_sdk_version = 28
        min_sdk_version = 16
        release_version = 1
        release_name = "1.0"

        ktlint_version = '6.2.1'
        kotlin_version = '1.2.71'
        koin_version = '1.0.2'
        timber_version = '4.6.0'

        support_version = '28.0.0'
        hotchemi_version = '3.3.1'
        constraint_version = '1.1.3'
        lottie_version = '2.7.0'

        rxandroid_version = '1.2.1'
        gson_version = '2.8.2'
        retrofit_version = '2.3.0'
    }

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:$ktlint_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { module ->
    if(module.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ktlint {
        version = "0.22.0"
        debug = false
        verbose = false
        android = true
        outputToConsole = false
        reporters = [ReporterType.CHECKSTYLE]
        ignoreFailures = true
    }

    android {
        compileSdkVersion rootProject.ext.target_sdk_version

        defaultConfig {
            if(module.name == 'app') {
                applicationId "de.zweidenker.connectivity"
            }
            minSdkVersion rootProject.ext.min_sdk_version
            targetSdkVersion rootProject.ext.target_sdk_version
            versionCode rootProject.ext.release_version
            versionName rootProject.ext.release_name
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "org.koin:koin-android:$koin_version"
        implementation "com.jakewharton.timber:timber:$timber_version"
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
